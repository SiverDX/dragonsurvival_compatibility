plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = minecraft_version + "-" + mod_version

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file("run/${it.name}")
            property 'forge.logging.markers', 'SCAN' /* REGISTRIES / REGISTRYDUMP */
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            // Support for mixins of other mods
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }

        clientRandom {
            parent runs.client
            // #### will be replaced with a random number | UUID is a randomly generated one
            args '--username', 'Dev####', '--uuid', 'fdb0e318-1ac3-4049-b6f8-d024c0b87a56'
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            // Support for mixins of other mods
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    flatDir { dirs 'libs' }
    maven { url = "https://jitpack.io" } // Mixin Squared
    maven { url = "https://www.cursemaven.com" } // CurseForge
    maven { url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" } // GeckoLib
    maven { url = "https://maven.blamejared.com/" } // JEI
    maven { url = "https://maven.theillusivec4.top/" } // Curios
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    // Core
    implementation fg.deobf("libs:dragonsurvival:${dragonsurvival_version}")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")

    // # --- Mods --- #
    // Cold Sweat
    compileOnly fg.deobf("curse.maven:cold-sweat-506194:5407819") // 1.20.1 - 2.3-b04a

    // Jade Support
    implementation fg.deobf("curse.maven:jade-324717:5390389") // 1.20.1 - 11.9.3

    // WTHIT Support
    compileOnly fg.deobf("curse.maven:wthit-forge-455982:5401194") // 1.20.1 - 8.12.0
    compileOnly fg.deobf("curse.maven:badpackets-615134:4784395") // 1.20.1 - 0.4.3 (dependency)

    // Player Animator (for Better Combat)
    compileOnly fg.deobf("curse.maven:playeranimator-658587:4587214") // 1.20.1 - 1.0.2

    // Forbidden and Arcanus
    compileOnly fg.deobf("curse.maven:forbidden-arcanus-309858:5198323") // 1.20.1 - 2.2.6
    compileOnly fg.deobf("curse.maven:valhelsia-core-416935:4735319") // 1.20.1 - 1.1.1 (dependency)

    // Apotheosis
    compileOnly fg.deobf("curse.maven:apotheosis-313970:5317303") // 1.20.1 - 7.3.5
    compileOnly fg.deobf("curse.maven:placebo-283644:5098182") // 1.20.1 - 8.6.1 (dependency)
    compileOnly fg.deobf("curse.maven:apothic-attributes-898963:4954000") // 1.20.1 - 1.2.1 (dependency)

    /* --- Mixin Configuration --- */
    implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixin_squared_version}"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:${mixin_squared_version}")) {
        jarJar.ranged(it, "[${mixin_squared_version},)")
    }

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}")) {
        jarJar.ranged(it, "[${mixin_extras_version},)")
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    config "${mod_id}.mixins.json"
    add sourceSets.main, "${mod_id}.refmap.json"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version: neo_version,
            neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id           : mod_id,
            mod_name         : mod_name,
            mod_license      : mod_license,
            mod_version      : mod_version,
            mod_authors      : mod_authors,
            mod_description  : mod_description,
            dragonsurvival_version_range: dragonsurvival_version_range
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : mod_version,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}